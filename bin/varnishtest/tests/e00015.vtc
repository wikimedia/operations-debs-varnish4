varnishtest "ESI requests turned off"

server s1 {
	rxreq
	expect req.url == /
	txresp -body {
		<html>
		Before include
		<esi:include src="/body"/>
		After include
	}
	rxreq
	txresp -body {
		<html>
		Before include
		<esi:include src="/body"/>
		After include
	}
	rxreq
	expect req.url == "/body"
	txresp -body {
		<pre>Included file</pre>
	}
} -start

varnish v1 -syntax 4.0 -vcl+backend {
	sub vcl_recv {
		if (req.url == "/") {
			set req.esi = false;
		}
	}
	sub vcl_backend_response {
		set beresp.do_esi = true;
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.bodylen == 73
	expect resp.status == 200
	txreq -url "/esi"
	rxresp
	expect resp.bodylen == 76
	expect resp.status == 200
} -run

varnish v1 -vsl_catchup

varnish v1 -expect esi_errors == 0

server s1 {
	rxreq
	expect req.url == /top2
	txresp -body {
		<html>
		Before include
		<esi:include src="/body"/>
		After include
	}
} -start

varnish v1 -syntax 4.1 -vcl+backend {
	sub vcl_deliver {
		set resp.http.was = resp.do_esi;
		if (req.url == "/top2") {
			set resp.do_esi = false;
		}
	}
	sub vcl_backend_response {
		set beresp.do_esi = true;
	}
}

client c1 {
	txreq -url /top2
	rxresp
	expect resp.bodylen == 73
	expect resp.status == 200
	expect resp.http.was == true
	txreq -url "/esi"
	rxresp
	expect resp.bodylen == 76
	expect resp.status == 200
	expect resp.http.was == true
} -run

varnish v1 -expect esi_errors == 0
